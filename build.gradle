import java.util.regex.Pattern

buildscript {
    ext.Sdk = [
        'MIN_SDK_VERSION': 21,
        'TARGET_SDK_VERSION': 29,
        'COMPILE_SDK_VERSION': 29
    ]

    ext.versions = [
            'AGP': '4.0.0',
            'DETEKT': '1.10.0',
            'KOTLIN': '1.3.72',
            'KTLINT': '9.2.1',
            'VERSIONS_PLUGIN': '0.28.0',
            'JUNIT': '4.13',
            'ANDROIDX_TEST': '1.2.0',
            'ANDROIDX_TEST_EXT': "1.1.1",
            'APPCOMPAT': '1.1.0',
            'CORE_KTX': '1.2.0'
    ]

    ext.deps = [
            'JUNIT': "junit:junit:${versions.JUNIT}",
            'ANDROIDX_APPCOMPAT': "androidx.appcompat:appcompat:${versions.APPCOMPAT}",
            'ANDROIDX_CORE_KTX': "androidx.core:core-ktx:${versions.CORE_KTX}",
            'ANDROIDX_TEST_RUNNER': "androidx.test:runner:${versions.ANDROIDX_TEST}",
            'ANDROIDX_TEST_EXT_JUNIT': "androidx.test.ext:junit:${versions.ANDROIDX_TEST_EXT}"
    ]
}

plugins {
    id 'com.android.application' version '4.0.0' apply false
    id 'com.android.library' version '4.0.0' apply false
//    kotlin 'android' version '1.3.72' apply false //add as classpath
    id 'io.gitlab.arturbosch.detekt'  version '1.10.0'
    id 'org.jlleitschuh.gradle.ktlint' version '9.2.1'
    id 'com.github.ben-manes.versions' version '0.33.0'
}

allprojects {
    group = 'com.ncorti.kotlin.template'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply {
        plugin("io.gitlab.arturbosch.detekt")
        plugin("org.jlleitschuh.gradle.ktlint")
    }

    ktlint {
        debug.set(false)
        version.set(versions.KTLINT)
        verbose.set(true)
        android.set(false)
        outputToConsole.set(true)
        ignoreFailures.set(false)
        enableExperimentalRules.set(true)
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }
    }

    detekt {
        config = rootProject.files("config/detekt/detekt.yml")
        reports {
            html {
                enabled = true
                destination = file("build/reports/detekt.html")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(candidate.version)
    }
}

static boolean isNonStable(String version) {
    return !Pattern.compile('^[0-9,.v-]+(-r)?$').matcher(version).matches()
}
